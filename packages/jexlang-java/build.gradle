plugins {
    id 'java'
    id 'application'
}

group = 'com.jexlang.java'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {

    implementation("org.antlr:antlr4-runtime:4.13.2")
    implementation("org.apache.commons:commons-math3:3.6.1")


    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

def generatedVersionDir = layout.buildDirectory.dir("generated/version")

tasks.register("generateVersionFile") {
    def outputDir = generatedVersionDir.get().asFile
    inputs.property("version", project.version)
    outputs.dir(outputDir)

    doLast {
        def versionFile = new File(outputDir, "com/jexlang/java/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
            package com.jexlang.java;

            public class Version {
                public static final String VERSION = "${project.version}";
            }
        """.stripIndent()
    }
}

// Hook generated source into the main source set
sourceSets {
    main {
        java {
            srcDir generatedVersionDir
        }
    }
}

// Make Java compile depend on generation
tasks.named("compileJava") {
    dependsOn("generateVersionFile")
}